plugins {
	id 'net.fabricmc.fabric-loom-remap' version "${loom_version}"
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
}

// Determine which version-specific source directory to use based on Minecraft version
def mcVersion = project.minecraft_version
def useOldApi = false
if (mcVersion == "1.21" || 
    mcVersion == "1.21.1" || 
    mcVersion == "1.21.2" || 
    mcVersion == "1.21.3" || 
    mcVersion == "1.21.4" || 
    mcVersion == "1.21.5" || 
    mcVersion == "1.21.6") {
    useOldApi = true
}

// Versioned source set for MC-version-specific code (e.g. GlobalFactionMixin).
// Loom's splitEnvironmentSourceSets() overwrites main's java.srcDirs, so versioned
// code would not be compiled if added to main. A separate source set is compiled
// with main's classpath and included in the mod JAR via loom.mods.
sourceSets {
	versioned {
		java {
			srcDirs = [useOldApi ? 'src/main/java-1.21-1.21.6' : 'src/main/java-1.21.7+']
		}
		compileClasspath = sourceSets.main.output + sourceSets.main.compileClasspath
		runtimeClasspath = sourceSets.main.output + sourceSets.main.runtimeClasspath
	}
}

loom {
	splitEnvironmentSourceSets()

	mods {
		"civil-war" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
			sourceSet sourceSets.versioned
		}
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	dependsOn tasks.named('versionedClasses')
	from sourceSets.versioned.output
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}